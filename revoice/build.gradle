import org.doomedsociety.gradlecpp.GradleCppUtils
import org.doomedsociety.gradlecpp.toolchain.icc.IccCompilerPlugin
import org.doomedsociety.gradlecpp.toolchain.icc.Icc
import org.doomedsociety.gradlecpp.cfg.ToolchainConfig
import org.doomedsociety.gradlecpp.msvc.MsvcToolchainConfig
import org.doomedsociety.gradlecpp.gcc.GccToolchainConfig
import org.doomedsociety.gradlecpp.cfg.ToolchainConfigUtils
import org.gradle.language.cpp.CppSourceSet
import org.gradle.language.rc.tasks.WindowsResourceCompile
import org.gradle.nativeplatform.NativeBinarySpec
import versioning.RevoiceVersionInfo
import gradlecpp.VelocityUtils

apply plugin: 'cpp'
apply plugin: 'windows-resources'
apply plugin: IccCompilerPlugin

project.ext.dep_opus = project(':dep/opus')
project.ext.dep_silk = project(':dep/silk')
project.ext.dep_speex = project(':dep/speex')

List<Task> getRcCompileTasks(NativeBinarySpec binary) {
	def linkTask = GradleCppUtils.getLinkTask(binary)

	def res = linkTask.taskDependencies.getDependencies(linkTask).findAll { Task t -> t instanceof WindowsResourceCompile }
	return res as List
}

void postEvaluate(NativeBinarySpec b) {
	if (GradleCppUtils.windows) {
		getRcCompileTasks(b).each { Task t ->
			t.dependsOn project.generateAppVersion
		}
	} else {
		// attach generateAppVersion task to all 'compile source' tasks
		GradleCppUtils.getCompileTasks(b).each { Task t ->
			t.dependsOn project.generateAppVersion
		}
	}
}

void setupToolchain(NativeBinarySpec b) {
	ToolchainConfig cfg = rootProject.createToolchainConfig(b)
	cfg.projectInclude(project, '', '/src')
	cfg.projectInclude(rootProject, '/dep/rehlsdk/common', '/dep/rehlsdk/engine', '/dep/rehlsdk/dlls', '/dep/rehlsdk/public', '/dep/metamod', '/revoice/public')

	if (cfg instanceof MsvcToolchainConfig) {
		cfg.compilerOptions.pchConfig = new MsvcToolchainConfig.PrecompiledHeadersConfig(
			enabled: true,
			pchHeader: 'precompiled.h',
			pchSourceSet: 'revoice_pch'
		)
		cfg.compilerOptions.args '/Ob2', '/Oi', '/GF', '/GR-'
		cfg.singleDefines('_CRT_SECURE_NO_WARNINGS')
		cfg.extraLibs 'ws2_32.lib'
	} else if (cfg instanceof GccToolchainConfig) {
		cfg.compilerOptions.pchConfig = new GccToolchainConfig.PrecompilerHeaderOptions(
			enabled: true,
			pchSourceSet: 'revoice_pch'
		)
		cfg.compilerOptions.languageStandard = 'c++11'
		cfg.defines([
				'_stricmp': 'strcasecmp',
				'_strnicmp': 'strncasecmp',
				'_vsnprintf': 'vsnprintf',
				'_snprintf': 'snprintf'
		])

		cfg.compilerOptions.args '-Qoption,cpp,--treat_func_as_string_literal_cpp', '-msse2', '-fp-model fast', '-fomit-frame-pointer', '-fvisibility=hidden', '-fvisibility-inlines-hidden', '-fno-rtti', '-g0', '-s'
	}

	ToolchainConfigUtils.apply(project, cfg, b)

	GradleCppUtils.onTasksCreated(project, 'postEvaluate', {
		postEvaluate(b)
	})
}

model {
	buildTypes {
		debug
		release
	}

	platforms {
		x86 {
			architecture "x86"
		}
	}

	toolChains {
		visualCpp(VisualCpp) {
		}
		icc(Icc) {
		}
	}

	components {
		revoice(NativeLibrarySpec) {
			targetPlatform 'x86'
			baseName GradleCppUtils.windows ? 'revoice_mm' : 'revoice_mm_i386'

			sources {

				revoice_pch(CppSourceSet) {
					source {
						srcDirs "src"
						include "precompiled.cpp"
					}

					exportedHeaders {
						srcDirs "include", "version"
					}

					lib project: ':dep/opus', library: 'opus', linkage: 'static'
					lib project: ':dep/silk', library: 'silk', linkage: 'static'
					lib project: ':dep/speex', library: 'speex', linkage: 'static'
				}

				revoice_src(CppSourceSet) {
					source {
						srcDirs "src", "public"
						include "**/*.cpp"

						exclude "precompiled.cpp"
						exclude "engine_api.cpp"
					}

					exportedHeaders {
						srcDirs "include", "version"
					}

					lib project: ':dep/opus', library: 'opus', linkage: 'static'
					lib project: ':dep/silk', library: 'silk', linkage: 'static'
					lib project: ':dep/speex', library: 'speex', linkage: 'static'
				}
				rc {
					source {
						srcDir "msvc"
						include "revoice.rc"
					}
					exportedHeaders {
						srcDirs "msvc"
					}
				}
			}

			binaries.all {
				NativeBinarySpec b -> project.setupToolchain(b)
			}
		}
	}
}

afterEvaluate {
	project.binaries.all {
		NativeBinarySpec binary ->
		Tool linker = binary.linker

		if (GradleCppUtils.windows) {
			linker.args "/DEF:${projectDir}\\msvc\\revoice.def"
		}
	}
}

task buildRelease {
	dependsOn binaries.withType(SharedLibraryBinarySpec).matching { SharedLibraryBinarySpec blib ->
		blib.buildable && blib.buildType.name == 'release'
	}
}

tasks.clean.doLast {
	project.file('version/appversion.h').delete()
}

task generateAppVersion {

	RevoiceVersionInfo verInfo = (RevoiceVersionInfo) rootProject.revoiceVersionInfo
	def tplFile = project.file('version/appversion.vm')
	def renderedFile = project.file('version/appversion.h')

	// check to up-to-date
	inputs.file tplFile
	inputs.file project.file('gradle.properties')
	outputs.file renderedFile

	// this will ensure that this task is redone when the versions change
	inputs.property('version', rootProject.version)
	inputs.property('commitDate', verInfo.asCommitDate())

	println "##teamcity[buildNumber '" + verInfo.asMavenVersion(false) + "']";

	doLast {
		def templateCtx = [
			verInfo: verInfo
		]

		def content = VelocityUtils.renderTemplate(tplFile, templateCtx)
		renderedFile.delete()
		renderedFile.write(content, 'utf-8')

		println 'The current Revoice maven version is ' + rootProject.version + ', url: (' + verInfo.commitURL + '' + verInfo.commitSHA + ')';
	}
}
